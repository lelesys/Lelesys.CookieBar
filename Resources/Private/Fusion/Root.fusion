root.globalContent {
	@position = 'start'
	condition = ${q(node).is('[instanceof Lelesys.CookieBar:GlobalContent]')}
	renderPath = '/rawContent'
}

prototype(Lelesys.CookieBar:CookieBar) {
	htmlTemplate = ${q(node).property('template')}
	posButtonStart =  ${String.indexOf(this.htmlTemplate, 'BUTTON_START') + 15}
	posButtonEnd =  ${String.indexOf(this.htmlTemplate, 'BUTTON_END') - 3}
	buttonHtml = ${String.substring(this.htmlTemplate, this.posButtonStart, this.posButtonEnd)}
	buttonHtml.@process.1 = ${String.replace(value, '###OK###', q(node).property('okButtonText'))}

	posMainStart =  ${String.indexOf(this.htmlTemplate, 'MAIN_START') + 13}
	posMainEnd =  ${String.indexOf(this.htmlTemplate, 'MAIN_END') - 3}
	finalHtml = ${String.substring(this.htmlTemplate, this.posMainStart, this.posMainEnd)}

	messageHtml = ${q(node).property('message')}
	messageHtml.@process.1 = ${String.replace(value, '###OK_BUTTON###', this.buttonHtml)}
	finalHtml.@process.1 = ${String.replace(value, '###TEXT###', this.messageHtml)}
	finalHtml.@process.2 = ${String.replace(value, '###POSITION###', q(node).property('domPosition'))}
	finalHtml.@process.3 = Neos.Neos:ConvertUris
}


prototype(Neos.Neos:Page) {
	cookieBar = Neos.Fusion:Collection
	cookieBar {
		collection = ${q(site).children('[instanceof Lelesys.CookieBar:GlobalContent]').children('main').children('[instanceof Lelesys.CookieBar:CookieBar]')}
		# Render every item by its own Fusion object
		itemRenderer = Neos.Neos:ContentCase
		itemName = 'node'
		iterationName = 'iterator'
		@position = 'before cookieBarJs'
		@if.onlyRenderWhenNotInBackend = ${documentNode.context.inBackend == false}
	}

	cookieBarJs = Neos.Fusion:Template
	cookieBarJs {
		templatePath = 'resource://Lelesys.CookieBar/Private/Templates/NodeTypes/CookieBar.html'
		sectionName = 'cookieBarJs'
		@position = 'before closingBodyTag'
		@if.onlyRenderWhenNotInBackend = ${documentNode.context.inBackend == false}
	}
}